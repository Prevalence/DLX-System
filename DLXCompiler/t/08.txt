	;此服务例程把一个以NULL（空，即x0000）结束的字符串写到控制台
	;它服务于PUTS服务调用（TRAP x08）
	;输入：R4是指向要输出的字符串的指针
	;
		.data	x00002C00
	SaveR2:	.space	4	;保存寄存器的存储单元
	SaveR3:	.space	4 
	SaveR4: .space	4 
	SaveR5:	.space	4
 	SaveR31:.space	4 
	DSR:	.word	xFFFF0008	;  DSR的起始地址
	DDR:	.word	xFFFF000C	; DDR的起始地址
	Prompt:	.asciiz  	" Input  a  character>"
	.align #2
	;		
		.text	x00002D00
		sw	SaveR2(r0) ,r2	;保存此例程需要的寄存器
		sw	SaveR3(r0) ,r3
		sw	SaveR4(r0) ,r4
		sw	SaveR5(r0) ,r5
		sw	SaveR31(r0) ,r31
	;
	;对字符串中的每一个字符进行循环
	;
	LOOP:	lb	r2,0(r4) 	;取得字符
		beqz	r2, Return	;如果是0，字符串结束
		lw	r5, DSR (r0) 
	L2: 	lw	r3,0(r5) 	; 测试输出寄存器是否就绪
		andi	r3,r3,#1
		beqz	r3,L2	;循环直到显示器就绪
 		lw	r5, DDR(r0)
		sw	0(r5),r2	;显示字符
		addi	r4,r4,#1	;指针加1
		j	LOOP	;获取下一个字符
	;
	;从服务调用请求返回
	Return: 	lw	r2,SaveR2(r0)	;将寄存器恢复为原先的值
		lw	r3,SaveR3(r0)
		lw	r4,SaveR4(r0)
		lw	r5,SaveR5(r0)
		lw	r31,SaveR31(r0)
		jr	r31	 ;从TRAP返回            
